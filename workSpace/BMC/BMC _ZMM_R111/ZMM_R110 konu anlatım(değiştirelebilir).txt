Bu görevin amacı bir bakım tablosu ve log tablosu oluşturduk. Bakım tablosuna 8003 stok kazandırma 8004 stok azaltma olarak belirledik. Örenğin 2010 deposu için stok kazandırma  var ise alvde eklediğimiz alanda 8003 dönüyor. Gui statuse bir buton ekledik.
BAPI kullanarak eğer fark deposu alanı dolu ise BAPI_GOODSMVT_CREATE bapisini kullanarak 311 işlem türü ile transfer deposuna stok aktarımı gerçekleştiriyor. ZMM_T185 tablosu ise log tablomuz.


 SELECT * FROM zmm_t184 INTO TABLE @DATA(lt_t184).
  SELECT * FROM zmm_t185 INTO TABLE @DATA(lt_t185).
  SORT lt_t184 BY symdp frktp sira.
  SORT lt_t185 BY zyil werks matnr lgort.
  LOOP AT gt_liste REFERENCE INTO DATA(r_liste).
    DATA(lv_frktp) = COND #( WHEN r_liste->stk_fark > 0 THEN 'K' ELSE 'A' ).
    READ TABLE lt_t184 INTO DATA(ls_t184)
      WITH KEY frktp = lv_frktp symdp = r_liste->lgort BINARY SEARCH.
    IF sy-subrc EQ 0.
      r_liste->frktp = lv_frktp.
      r_liste->frkdp = ls_t184-frkdp.
    ENDIF.
    READ TABLE lt_t185 INTO DATA(ls_t185)
       WITH KEY zyil = p_vers werks = r_liste->werks matnr = r_liste->matnr lgort = r_liste->lgort BINARY SEARCH.
    IF sy-subrc EQ 0.
      r_liste->mblnr = ls_t185-mblnr.
      r_liste->mjahr = ls_t185-mjahr.
    ENDIF.
  ENDLOOP.


      CLEAR:gt_return.
      LOOP AT gt_liste INTO gs_liste WHERE selkz = 'X'.
        IF gs_liste-frkdp IS INITIAL.
          CLEAR: ls_return.
          ls_return-id          = '001'.
          ls_return-type        = 'E'.
          ls_return-message_v1  = 'Fark Deposu Alanı Boş'.
          APPEND ls_return TO gt_return.
*          MESSAGE 'Fark Deposu alanı boş' TYPE 'E'.
          CONTINUE.
        ENDIF.

        IF gs_liste-mblnr IS NOT INITIAL AND gs_liste-mjahr IS NOT INITIAL.
          CLEAR: ls_return.
          ls_return-id          = '001'.
          ls_return-type        = 'E'.
          ls_return-message_v1  = 'Bu malzeme ve depoya ait transfer'.
          ls_return-message_v2  = 'kaydı daha önce oluşturulmuştur.'.
          APPEND ls_return TO gt_return.
          CONTINUE.
        ENDIF.


        PERFORM call_bapi.

        MODIFY gt_liste FROM gs_liste.
      ENDLOOP.
      IF sy-subrc IS NOT INITIAL.
        CLEAR: ls_return.
        ls_return-id          = '001'.
        ls_return-type        = 'E'.
        ls_return-message_v1  = 'Hiçbir satır seçilmedi'.
*        MESSAGE 'Hiçbir satır seçilmedi' TYPE 'I'.
        APPEND ls_return TO gt_return.
      ENDIF.


      IF gt_return IS NOT INITIAL.
        CALL METHOD cl_rmsl_message=>display( gt_return ).
      ENDIF.


FORM call_bapi .
  DATA:ls_head    TYPE bapi2017_gm_head_01,
       lt_items   TYPE TABLE OF bapi2017_gm_item_create,
       ls_code    TYPE bapi2017_gm_code,
       ls_items   TYPE bapi2017_gm_item_create,
       lv_mblnr   TYPE mkpf-mblnr,
       lv_mjahr   TYPE mkpf-mjahr,
       ls_headret TYPE bapi2017_gm_head_ret.

  DATA:ls_return LIKE bapiret2,
       lt_return TYPE TABLE OF bapiret2.
  DATA:ls_t185 TYPE zmm_t185.


  ls_head-pstng_date = sy-datum.
  ls_head-doc_date   = sy-datum.

  CLEAR: lt_items, ls_items.
  ls_code-gm_code     = '04'.
  ls_items-material   = gs_liste-matnr.
  ls_items-plant      = gs_liste-werks.
  ls_items-stge_loc   = gs_liste-lgort.
  ls_items-move_stloc = gs_liste-frkdp.
  ls_items-entry_qnt  = gs_liste-stk_fark.
  ls_items-entry_uom  = gs_liste-meins.
  ls_items-move_type  = '311'.
  APPEND ls_items TO lt_items.

  CLEAR: lt_return.
  CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
    EXPORTING
      goodsmvt_header  = ls_head
      goodsmvt_code    = ls_code
    IMPORTING
      materialdocument = lv_mblnr
      matdocumentyear  = lv_mjahr
      goodsmvt_headret = ls_headret
    TABLES
      goodsmvt_item    = lt_items
      return           = lt_return.

  LOOP AT lt_return INTO ls_return WHERE type CA 'AEX'.
    EXIT.
  ENDLOOP.
  IF sy-subrc IS INITIAL.
    CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
  ELSE.
    gs_liste-mblnr = ls_headret-mat_doc.
    gs_liste-mjahr = ls_headret-doc_year.

    CLEAR:ls_t185.
    MOVE-CORRESPONDING gs_liste TO ls_t185.
    ls_t185-zyil = p_vers.
    MODIFY zmm_t185 FROM ls_t185.
    CLEAR: ls_return.
    ls_return-id          = '001'.
    ls_return-type        = 'S'.
    ls_return-message_v1  = lv_mblnr && ' nolu malzeme başarıyla oluşturuldu.'.
    APPEND ls_return TO gt_return.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.
  ENDIF.
  APPEND LINES OF lt_return TO gt_return.
ENDFORM.